Import('env', 'envCython', 'arch')

common_libs = [
  'params.cc',
  'swaglog.cc',
  'util.cc',
  'ratekeeper.cc',
  'clutil.cc',
]

_common = env.Library('common', common_libs, LIBS="json11")
Export('_common')

if GetOption('extras'):
  env.Program('tests/test_common',
              ['tests/test_runner.cc', 'tests/test_params.cc', 'tests/test_util.cc', 'tests/test_swaglog.cc'],
              LIBS=[_common, 'json11', 'zmq', 'pthread'])

# C++ bindings
import platform

params_c_linkflags = []
if platform.system() == "Darwin":
  params_c_linkflags = ["-bundle", "-undefined", "dynamic_lookup"]
else:
  params_c_linkflags = ["-shared"]

params_c = env.Program('libparams_c.so', ['params_capi.cc'], LIBS=[_common, 'zmq', 'json11'], LINKFLAGS=params_c_linkflags)

SConscript([
  'transformations/SConscript',
])

Import('transformations_python')
common_python = [params_c, transformations_python]

Export('common_python')
